# -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ([2.63])
AC_INIT([makedoc], [0.1], [florian@linbit.com])
AM_INIT_AUTOMAKE

AC_CONFIG_MACRO_DIR([m4])

AC_CANONICAL_HOST

AC_LANG([C])

# local options
AC_ARG_ENABLE([html],
	[AS_HELP_STRING([--disable-html],
			[disable HTML output (default: enabled)])])
AM_CONDITIONAL(ENABLE_HTML, [ test x$enable_html != "xno" ])

AC_ARG_ENABLE([pdf],
	[AS_HELP_STRING([--disable-pdf],
			[disable PDF output (default: enabled)])])
AM_CONDITIONAL(ENABLE_PDF, [ test x$enable_pdf != "xno" ])

AC_ARG_ENABLE([odf],
	[AS_HELP_STRING([--disable-odf],
			[disable OpenDocument Format output (default: enabled)])])
AM_CONDITIONAL(ENABLE_ODF, [ test x$enable_odf != "xno" ])

AC_ARG_ENABLE([doc],
	[AS_HELP_STRING([--disable-doc],
			[disable Microsoft Word output (default: enabled)])])
AM_CONDITIONAL(ENABLE_DOC, [ test x$enable_doc != "xno" ])

AC_ARG_ENABLE([asciidoc],
	[AS_HELP_STRING([--disable-asciidoc],
			[disable AsciiDoc input (default: enabled)])])
AM_CONDITIONAL(ENABLE_ASCIIDOC, [ test x$enable_asciidoc != "xno" ])

AC_ARG_ENABLE([asciidoc-docinfo],
    [AS_HELP_STRING([--enable-asciidoc-docinfo],
       [include AsciiDoc docinfo files (if they exist, default: disabled)])])
AM_CONDITIONAL(ENABLE_ASCIIDOC_DOCINFO, [ test x$enable_asciidoc_docinfo = "xyes" ])

AC_ARG_ENABLE([mathml],
	[AS_HELP_STRING([--disable-mathml],
			[disable MathML support (default: enabled)])])
AM_CONDITIONAL(ENABLE_MATHML, [ test x$enable_mathml != "xno" ])

AC_ARG_ENABLE([profiling],
	[AS_HELP_STRING([--enable-profiling],
			[enable document profiling (default: disabled)])])
AM_CONDITIONAL(PROFILING, [ test x$enable_profiling = "xyes" ])

AC_ARG_ENABLE([comments],
	[AS_HELP_STRING([--enable-comments],
			[include comments (remarks) in output (default: disabled)])])
AM_CONDITIONAL(COMMENTS, [ test x$enable_comments = "xyes" ])

AC_ARG_ENABLE([draft-mode],
	[AS_HELP_STRING([--enable-draft-mode],
			[assume any document is a draft (default: disabled)])])
AM_CONDITIONAL(DRAFT_MODE, [ test "x$enable_draft_mode" = "xyes" ])

AC_ARG_ENABLE([html-chunking],
	[AS_HELP_STRING([--enable-html-chunking],
			[create multiple chunks for HTML output (default: disabled)])])
AM_CONDITIONAL(HTML_CHUNK, [ test "x$enable_html_chunking" = "xyes" ])

AC_ARG_ENABLE([section-numbers],
	[AS_HELP_STRING([--enable-section-numbers],
			[enable section numbering (default: disabled)])])
AM_CONDITIONAL(SECTION_NUMBERS, [ test "x$enable_section_numbers" = "xyes" ])

AC_ARG_ENABLE([syntax-highlighting],
	[AS_HELP_STRING([--enable-syntax-highlighting],
			[enable syntax highlighting for source code listings (not supported by all processors/formatters, default: disabled)])])
AM_CONDITIONAL(SYNTAX_HIGHLIGHTING, [ test "x$enable_syntax_highlighting" = "xyes" ])

AC_ARG_WITH([section-number-depth],
    [AS_HELP_STRING([--section-number-depth=NUM],
       [number of levels to use for section numbering (default: 2, ignored unless --enable-section-numbers is also set)])],
	[ SECTION_NUMBER_DEPTH="$withval" ],
	[ SECTION_NUMBER_DEPTH="2" ])
AC_SUBST(SECTION_NUMBER_DEPTH)

AC_ARG_WITH([stylesheets],
    [AS_HELP_STRING([--with-stylesheets=URL],
       [location of the DocBook XSL distribution])],
	[ STYLESHEET_PREFIX="$withval" ],
	[ STYLESHEET_PREFIX="http://docbook.sourceforge.net/release/xsl/current" ])
AC_SUBST(STYLESHEET_PREFIX)

AC_ARG_WITH([asciidoc-extension],
    [AS_HELP_STRING([--with-asciidoc-extension=NAME],
       [expect this extension for AsciiDoc filenames (default txt)])],
	[ ASCIIDOC_EXTENSION="$withval" ],
	[ ASCIIDOC_EXTENSION="txt" ])
AC_SUBST(ASCIIDOC_EXTENSION)

AC_ARG_WITH([asciidoc-doctype],
    [AS_HELP_STRING([--with-asciidoc-type=article|book|manpage],
       [assume this document type for AsciiDoc files (default article)])],
	[ ASCIIDOC_DOCTYPE="$withval" ],
	[ ASCIIDOC_DOCTYPE="article" ])
AC_SUBST(ASCIIDOC_DOCTYPE)

AC_ARG_WITH([ulink-target],
    [AS_HELP_STRING([--with-ulink-target=NAME],
       [target frame for external links in HTML output (default _blank)])],
	[ ULINK_TARGET="$withval" ],
	[ ULINK_TARGET="_blank" ])
AC_SUBST(ULINK_TARGET)

AC_ARG_WITH([css],
    [AS_HELP_STRING([--with-css=NAME],
       [CSS style sheet for HTML output (default default.css)])],
	[ CSS="$withval" ],
	[ CSS="default.css" ])
AC_SUBST(CSS)

AC_ARG_WITH([html-flavor],
    [AS_HELP_STRING([--with-html-flavor=html|xhtml],
       [select output HTML flavor (default xhtml)])],
	[ HTML_FLAVOR="$withval" ],
	[ HTML_FLAVOR="xhtml" ])
AC_SUBST(HTML_FLAVOR)

AC_ARG_WITH([xslt-processor],
    [AS_HELP_STRING([--with-xslt-processor=xsltproc|saxon-xslt],
       [use this processor for rendering DocBook XML to FO and HTML (default: xsltproc)])],
	[ XSLT_PROCESSOR="$withval" ],
	[ XSLT_PROCESSOR="xsltproc" ])
AC_SUBST(XSLT_PROCESSOR)
AM_CONDITIONAL(USE_XSLTPROC, [ test "x$XSLT_PROCESSOR" = "xxsltproc"])
AM_CONDITIONAL(USE_SAXON, [ test "x$XSLT_PROCESSOR" = "xsaxon-xslt"])

AC_ARG_WITH([pdf-formatter],
    [AS_HELP_STRING([--with-pdf-formatter=fop|xmlroff|dblatex],
       [use this formatter for rendering FO to PDF (default: fop)])],
	[ PDF_FORMATTER="$withval" ],
	[ PDF_FORMATTER="fop" ])
AC_SUBST(PDF_FORMATTER)
AM_CONDITIONAL(USE_FOP, [test "x$enable_pdf" != "xno" && test "x$PDF_FORMATTER" = "xfop"])
AM_CONDITIONAL(USE_XMLROFF, [test "x$enable_pdf" != "xno" && test "x$PDF_FORMATTER" = "xxmlroff"])
AM_CONDITIONAL(USE_DBLATEX, [test "x$enable_pdf" != "xno" && test "x$PDF_FORMATTER" = "xdblatex"])

AC_ARG_WITH([pdf-toc],
    [AS_HELP_STRING([--with-pdf-toc=STRING],
       [control ToC generation for PDF output (see http://docbook.sourceforge.net/release/xsl/current/doc/html/generate.toc.html for supported values)])],
	[ FO_TOC="$withval" ],
	[ FO_TOC="/set toc,title /book toc,title /article nop" ])
AC_SUBST(FO_TOC)

AC_ARG_WITH([pdf-body-font],
    [AS_HELP_STRING([--with-pdf-body-font=NAME],
       [body font for PDF output (default serif)])],
	[ FO_BODY_FONT_FAMILY="$withval" ],
	[ FO_BODY_FONT_FAMILY="serif" ])
AC_SUBST(FO_BODY_FONT_FAMILY)

AC_ARG_WITH([pdf-title-font],
    [AS_HELP_STRING([--with-pdf-title-font=NAME],
       [title font for PDF output (default: same as body font)])],
	[ FO_TITLE_FONT_FAMILY="$withval" ],
	[ FO_TITLE_FONT_FAMILY="$FO_BODY_FONT_FAMILY" ])
AC_SUBST(FO_TITLE_FONT_FAMILY)

AC_ARG_WITH([pdf-paper-size],
    [AS_HELP_STRING([--with-pdf-paper-size=SIZE],
       [set paper size for PDF output (default A4)])],
	[ PAPER_SIZE="$withval" ],
	[ PAPER_SIZE="A4" ])
AC_SUBST(PAPER_SIZE)

AC_ARG_WITH([pdf-titlepage],
    [AS_HELP_STRING([--with-pdf-titlepage=PATH],
       [set path for the DocBook title page XML file (default: none)])],
	[ FO_TITLEPAGE="$withval" ])
AC_SUBST(FO_TITLEPAGE)
AM_CONDITIONAL(USE_FO_TITLEPAGE, [test -n "$FO_TITLEPAGE"])

AC_ARG_WITH([fop-config],
    [AS_HELP_STRING([--with-fop-config=PATH],
       [location of the FOP configuration file (no default)])],
	[ FOP_CONFIG="$withval" ])
if test -n "$FOP_CONFIG"; then
   FOP_OPTIONS='-c $(FOP_CONFIG)'
fi
AC_SUBST(FOP_CONFIG)
AC_SUBST(FOP_OPTIONS)

AC_ARG_WITH([xmlroff-backend],
    [AS_HELP_STRING([--with-xmlroff-backend=gp|cairo],
       [use this xmlroff backend for rendering FO to PDF (default gp)])],
	[ XMLROFF_BACKEND="$withval" ],
	[ XMLROFF_BACKEND="gp" ])
AC_SUBST(XMLROFF_BACKEND)

AC_ARG_WITH([dblatex-backend],
    [AS_HELP_STRING([--with-dblatex-backend=pdftex|dvips|xetex],
       [use this dblatex backend for rendering DocBook XML to PDF (default pdftex)])],
	[ DBLATEX_BACKEND="$withval" ],
	[ DBLATEX_BACKEND="pdftex" ])
AC_SUBST(DBLATEX_BACKEND)

AC_ARG_WITH([svg-rasterizer],
    [AS_HELP_STRING([--with-svg-rasterizer=inkscape|rsvg],
       [use this rasterizer for rendering SVG to PNG (default: inkscape)])],
	[ SVG_RASTERIZER="$withval" ],
	[ SVG_RASTERIZER="inkscape" ])
AC_SUBST(SVG_RASTERIZER)
AM_CONDITIONAL(USE_INKSCAPE, [test "x$SVG_RASTERIZER" = "xinkscape"])
AM_CONDITIONAL(USE_RSVG, [test "x$SVG_RASTERIZER" = "xrsvg"])

AC_ARG_WITH([odf-template],
    [AS_HELP_STRING([--with-odf-template=PATH],
       [use this OpenDocument template (default: none)])],
	[ ODF_TEMPLATE="$withval" ])
AC_SUBST(ODF_TEMPLATE)
AM_CONDITIONAL(USE_ODF_TEMPLATE, [test "x$ODF_TEMPLATE" != "x"])

dnl DocBook document profiling options
AC_ARG_WITH([profile-arch],
    [AS_HELP_STRING([--with-profile-arch=NAME],
       [set architecture (for document profiling)])],
	[ XSLTPROC_PROFILING_OPTIONS="--stringparam profile.arch $withval" ])
AC_ARG_WITH([profile-condition],
    [AS_HELP_STRING([--with-profile-condition=NAME],
       [set condition (for document profiling)])],
	[ XSLTPROC_PROFILING_OPTIONS="$XSLTPROC_PROFILING_OPTIONS --stringparam profile.condition $withval" ])
AC_ARG_WITH([profile-userlevel],
    [AS_HELP_STRING([--with-profile-userlevel=NAME],
       [set user level (for document profiling)])],
	[ XSLTPROC_PROFILING_OPTIONS="$XSLTPROC_PROFILING_OPTIONS --stringparam profile.userlevel $withval" ])
AC_ARG_WITH([profile-vendor],
    [AS_HELP_STRING([--with-profile-vendor=NAME],
       [set vendor (for document profiling)])],
	[ XSLTPROC_PROFILING_OPTIONS="$XSLTPROC_PROFILING_OPTIONS --stringparam profile.vendor $withval" ])
AC_ARG_WITH([profile-status],
    [AS_HELP_STRING([--with-profile-status=NAME],
       [set document status (for document profiling, default current)])],
	[ XSLTPROC_PROFILING_OPTIONS="$XSLTPROC_PROFILING_OPTIONS --stringparam profile.status $withval" ],
	[ XSLTPROC_PROFILING_OPTIONS="$XSLTPROC_PROFILING_OPTIONS --stringparam profile.status current" ])

AC_SUBST(XSLTPROC_PROFILING_OPTIONS)


# Checks for programs.
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_SED

dnl xsltproc or Saxon -- for XSLT transformation
case "x$XSLT_PROCESSOR" in
     "xxsltproc")
	AC_CHECK_PROGS([XSLTPROC], [xsltproc])
	if test -z $XSLTPROC; then
	   AC_MSG_WARN([xsltproc not found, cannot transform XML])
	fi
	;;
     "xsaxon-xslt")
	AC_CHECK_PROGS([SAXON], [saxon-xslt])
	if test -z $SAXON; then
	   AC_MSG_WARN([Saxon not found, cannot transform XML])
	fi
	;;
esac
AM_CONDITIONAL([XSLT], [ test -n "$XSLTPROC" || test -n "$SAXON" ])
AM_CONDITIONAL([RENDER_HTML], [ test x$enable_html != "xno" ])

dnl xmllint -- for XML validation
AC_CHECK_PROGS([XMLLINT], [xmllint])
if test -z $XMLLINT; then
	AC_MSG_WARN([xmllint not found, cannot validate XML])
fi
AM_CONDITIONAL([VALIDATE], [ test -n "$XMLLINT"])

dnl asciidoc -- for generating DocBook XML from asciidoc
if test "x$enable_asciidoc" != "xno"; then
   AC_CHECK_PROGS([ASCIIDOC], [asciidoc])
   if test -z $ASCIIDOC; then
	AC_MSG_WARN([asciidoc not found, cannot build from asciidoc sources])
   fi
fi
AM_CONDITIONAL([ASCIIDOC_SUPPORT], [ test -n "$ASCIIDOC" ])

if test "x$enable_pdf" != "xno"; then
   case "x$PDF_FORMATTER" in
     "xfop")
	AC_CHECK_PROGS([FOP], [fop])
	if test -z $FOP; then
	   AC_MSG_WARN([fop not found, cannot render PDF])
	fi
	AC_CHECK_PROGS([FOP_TTFREADER], [fop-ttfreader])
	if test -z $FOP_TTFREADER; then
	   AC_MSG_WARN([fop-ttfreader not found, cannot generate font metrics files from TTF])
	fi
	;;
     "xxmlroff")
	AC_CHECK_PROGS([XMLROFF], [xmlroff])
	if test -z $XMLROFF; then
	   AC_MSG_WARN([xmlroff not found, cannot render PDF])
	fi
	;;
     "xdblatex")
	AC_CHECK_PROGS([DBLATEX], [dblatex])
	if test -z $DBLATEX; then
	   AC_MSG_WARN([dblatex not found, cannot render PDF])
	fi
   esac
fi
AM_CONDITIONAL([FONT_METRICS_TTF], [ test -n "$FOP_TTFREADER" ])
AM_CONDITIONAL([RENDER_PDF], [ test -n "$FOP" || test -n "$XMLROFF" || test -n "$DBLATEX" ])
AM_CONDITIONAL([RENDER_PS], [ test -n "$FOP" || test -n "$XMLROFF" || test -n "$DBLATEX" ])

dnl docbook2xml -- for creating OpenDocument Text from DocBook
if test "x$enable_odf" != "xno"; then
   AC_CHECK_PROGS([DOCBOOK2ODF], [docbook2odf])
   if test -z $DOCBOOK2ODF; then
      AC_MSG_WARN([docbook2odf not found, cannot render OpenDocument Text])
   fi
fi
AM_CONDITIONAL([RENDER_ODT], [ test -n "$DOCBOOK2ODF" ])

dnl unoconv -- for creating Microsoft Word compatible document from ODF
if test "x$enable_doc" != "xno"; then
   AC_CHECK_PROGS([UNOCONV], [unoconv])
   if test -z $UNOCONV; then
      AC_MSG_WARN([unoconf not found, cannot render Microsoft Word compatible documents])
   fi
fi
AM_CONDITIONAL([RENDER_DOC], [ test -n "$UNOCONV" ])

dnl Inkscape or rsvg -- for rasterizing PNGs from SVG
case "x$SVG_RASTERIZER" in
     "xinkscape")
	AC_CHECK_PROGS([INKSCAPE], [inkscape])
	if test -z $INKSCAPE; then
	   AC_MSG_WARN([inkscape not found, cannot rasterize SVG graphics])
	fi
	;;
     "xrsvg")
	AC_CHECK_PROGS([RSVG], [rsvg])
	if test -z $RSVG; then
	   AC_MSG_WARN([rsvg not found, cannot rasterize SVG graphics])
	fi
	;;
esac
AM_CONDITIONAL([RENDER_SVG], [ test -n "$INKSCAPE" || test -n "$RSVG" ])

dnl mathmlsvg -- for generating SVG from MML
if test "x$enable_mathml" != "xno"; then
   AC_CHECK_PROGS([MATHMLSVG], [mathmlsvg])
   if test -z $MATHMLSVG; then
      AC_MSG_WARN([mathmlsvg not found, cannot generate SVG graphics from MML])
   fi
fi
AM_CONDITIONAL([RENDER_MML], [ test -n "$MATHMLSVG" ])

# Checks for libraries.

# Checks for header files.

# Checks for typedefs, structures, and compiler characteristics.

# Checks for library functions.

# Checks for files
if test -n "$FOP_CONFIG"; then
   AC_CHECK_FILE([$FOP_CONFIG],
   ,
   AC_MSG_ERROR([Cannot find FOP configuration file $FOP_CONFIG]))
fi
if test -n "$FO_TITLEPAGE"; then
   AC_CHECK_FILE([$FO_TITLEPAGE],
   ,
   AC_MSG_ERROR([Cannot find PDF titlepage XML $FO_TITLEPAGE]))
fi

## random vars

## Feature summary
AC_MSG_NOTICE()
AC_MSG_NOTICE([Feature summary])
AC_MSG_NOTICE([===============])
AM_COND_IF([RENDER_HTML],
           AC_MSG_NOTICE([HTML output is enabled]),
           AC_MSG_NOTICE([HTML output is not enabled (install xsltproc)]))
AM_COND_IF([RENDER_PDF],
           AC_MSG_NOTICE([PDF output is enabled]),
	   AC_MSG_NOTICE([PDF output is not enabled (install fop or xmlroff)]))
AM_COND_IF([RENDER_PS],
           AC_MSG_NOTICE([PostScript output is enabled]),
	   AC_MSG_NOTICE([PostScript output is not enabled (install fop or xmlroff)]))
AM_COND_IF([USE_FOP],
           AC_MSG_NOTICE([Using FOP for PDF/PS output]))
AM_COND_IF([USE_XMLROFF],
           AC_MSG_NOTICE([Using xmlroff for PDF/PS output]))
AM_COND_IF([ASCIIDOC_SUPPORT],
           AC_MSG_NOTICE([AsciiDoc input is enabled]),
	   AC_MSG_NOTICE([AsciiDoc input is not enabled (install asciidoc)]))
AM_COND_IF([RENDER_ODT],
           AC_MSG_NOTICE([OpenDocument Text output is enabled]),
	   AC_MSG_NOTICE([OpenDocument Text output is not enabled (install docbook2odf)]))
AM_COND_IF([RENDER_DOC],
           AC_MSG_NOTICE([Microsoft Word output is enabled]),
	   AC_MSG_NOTICE([Microsoft Word output is not enabled (install unoconv)]))
AM_COND_IF([RENDER_SVG],
           AC_MSG_NOTICE([SVG rasterization is enabled]),
	   AC_MSG_NOTICE([SVG rasterization is not enabled (install inkscape or rsvg)]))
AM_COND_IF([USE_INKSCAPE],
           AC_MSG_NOTICE([Using inkscape for SVG rasterization]))
AM_COND_IF([USE_RSVG],
           AC_MSG_NOTICE([Using rsvg for SVG rasterization]))
AM_COND_IF([RENDER_MML],
           AC_MSG_NOTICE([MathML rendering is enabled]),
	   AC_MSG_NOTICE([MathML rendering is not enabled (install mathmlsvg)]))
AM_COND_IF([SECTION_NUMBERS],
           AC_MSG_NOTICE([Section numbering is enabled]),
	   AC_MSG_NOTICE([Section numbering is not enabled]))

AC_MSG_NOTICE()


## do subst
